import os 
import re
import glob

""" Snakerules for running 10x cellranger """

def _collect_groups(wildcards):
    """generate a csv file with path to molecule_info.h5 for each expt
    group. Return group_id and path to csv as a list """
     
    groups = wildcards.expt_groups
    data = wildcards.data

    groups = groups.split(",")
    outdir = os.path.join(data, "results")

    out = open(groups[0] + "_groups_def.csv", 'w')
    out.write("sample_id,molecule_h5\n") # recent cellranger changed to us sample_id instead of library_id
    for group in groups: 
        if group is "":
            continue
        library = group.strip(" ")
        path = os.path.join(outdir, library, "outs", "molecule_info.h5")
        out.write("{},{}\n".format(library, path))
    
    out.close()
    # return list with group id and path to csv
    return [groups[0][:-1] + "_aggr", groups[0] + "_groups_def.csv"]


def _get_sample_ids(wildcards):
    """ extract out sample prefixes from fastqs to pass to cellranger
    i.e. given the following fastqs
    sample_name_1_S1_L001_R1_001.fastq.gz"
    sample_name_2_S2_L001_R1_001.fastq.gz"
    sample_name_3_S3_L001_R1_001.fastq.gz"

    return:
    sample_name_1,sample_name_2,sample_name_3
    """
    fq_regex = "_S[0-9]+_L[0-9]{3}_[RI][12]_001\.fastq\.gz$"

    fqs = _get_sample_fq_paths(wildcards)
    R1_fqs = [x for x in fqs if "R1" in x]
    R1_fqs = [os.path.basename(x) for x in R1_fqs]

    sample_ids = set()
    for fq in R1_fqs:
      if re.search(fq_regex, fq) is not None:
        sample_id = re.sub(fq_regex, "", fq)
        sample_ids.add(sample_id)
      else:
        sys.exit("unable to parse out sample id from {}".format(fq))
    
    return ",".join(sample_ids)

def _get_sample_fq_paths(wildcards):
    
    fq_regex = "(_[1-4]_|_)S[0-9]+_L[0-9]{3}_[RI][12]_001\.fastq\.gz$"
    
    fq_dir = RAW_DATA
     
    fqs = glob.glob(os.path.join(fq_dir, wildcards.sample + '*'))

    res = set()
    for fq in fqs:
      m = re.fullmatch(wildcards.sample + fq_regex,
                       os.path.basename(fq))
      if m:
          res.add(fq)
    # sort to ensure that R1 is before R2
    res = sorted(list(res))

    if len(res) == 0:
      sys.exit("no fastqs found for sample {}".format(wildcards.sample))
    elif len(res) not in [2, 8]:
      print("# of fastqs is non-standard, # = {}".format(str(len(res))),
            file = sys.stderr)

    print("running cellranger on the following fqs\nfastq\tsample", file = sys.stderr) 
    for fq in res:
      print("{}\t{}".format(fq, wildcards.sample), file = sys.stderr)

    return res

rule cellranger_count:
    """ note that cellranger count will fail if the output directory
    specified with ``--id`` exists prior to running. By default Snakemake 
    will generate directories listed in the input and output params, so to
    avoid this error this rule creates a dummy file "{sample}_complete.txt """

    input:
      _get_sample_fq_paths
    output:
      "{data}/results/logs/{sample}_complete.txt"
    params:
      fqs = _get_sample_ids,
      outdir = "{data}/results/",
      indir =  RAW_DATA, 
      job_name = "count",
      memory = "select[mem>4] rusage[mem=4]",
    log: "{data}/logs/{sample}_count.out"
    threads: 2
    shell:
      """
      set -x
      echo {params.fqs}
      cellranger count \
          --id={wildcards.sample} \
          --fastqs={params.indir} \
          --sample={params.fqs} \
          --jobmode={LSF_TEMPLATE} \
          --project={PROJECT} \
          --maxjobs={MAX_10X_JOBS} \
          --transcriptome={TRANSCRIPTOME}

      mkdir -p {params.outdir}
      mv -u {wildcards.sample} {params.outdir}
      
      # test if output bam file produced by count
      bam="{params.outdir}{wildcards.sample}/outs/possorted_genome_bam.bam" 

      if [ -s $bam ] 
        then 
        echo "finished" > {output}
      fi
      """ 

rule cellranger_aggr:
    input:
      expand("{data}/results/logs/{sample}_complete.txt",
        sample = SAMPLES, 
        data = DATA),
    output:
      "{data}/results/logs/{expt_groups}_aggr_complete.out.txt"
    params:
      groups = _collect_groups,
      outdir = "{data}/results/",
      job_name = "aggr",
      memory = "select[mem>4] rusage[mem=4]",
    log: "{data}/logs/{expt_groups}_aggr.out"
    threads: 2
    shell:
      """
      set -x
      cellranger aggr \
          --csv={params.groups[1]} \
          --id={params.groups[0]} \
          --jobmode={LSF_TEMPLATE} \
          --maxjobs={MAX_10X_JOBS} 
      
      mkdir -p {params.outdir}
      mv -u {params.groups[0]} {params.outdir}
      mv -i {params.groups[1]} {params.outdir}      
      
      summary_html="{params.outdir}{params.groups[0]}/outs/web_summary.html"
      if [ -s $summary_html ]
      then
        echo "finished" > {output}
      fi
      """ 
